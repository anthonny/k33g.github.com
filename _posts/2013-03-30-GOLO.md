---

layout: post
title: papa maman font gologolo
info : gologolo

---

#Quand Papa et Maman font gologolo ...

> *Ou, il y a héritage et apprentissage ...*
> *Attention, article torride ...*

##Rappel sur les DynamicObjects + mixin

*[cf. article précédent](http://k33g.github.com/2013/03/28/GOLO.html)*

Ok, elle était facile ... Mais voyons comment aller un peu plus loin avec les **DynamicObjects** par l'exemple.

Nous avons un "FlintStone" :

{% highlight coffeescript %}
function FlintStone = -> DynamicObject():
	lastName("Flintstone"):
	define("initialize", |this, firstName|{
		this: firstName(firstName)
		return this
	})
{% endhighlight %}

Commençons par créer un couple de "FlintStone" :

{% highlight coffeescript %}
function Father = -> DynamicObject():
	mixin(FlintStone()):
	hair("brown"):
	sex("male"):
	define("fishing", |this| -> println(this: firstName() + " is fishing") )


function Mother = -> DynamicObject():
	mixin(FlintStone()):
	hair("red"):
	sex("female"):
	define("drawing", |this| -> println(this: firstName() + " is drawing") )
{% endhighlight %}

Donc les femmes "FlintStone" savent dessiner et sont rousses, les hommes "FlintStone" savent pêcher et sont bruns. `Father` et `Mother` "descendent" tous kes 2 de `FlintStone`. 

Donc ceci :

{% highlight coffeescript %}
function main = |args| {

	var fred = Father(): initialize("Fred")
	var wilma = Mother(): initialize("Wilma")

	fred: fishing()
	wilma: drawing()

}
{% endhighlight %}

vous donnera cela :

	Fred is fishing
	Wilma is drawing

##Wilma et Fred vont faire des bébés : ou Golo & le multi-héritage

Une "petite" chose que j'ai oublié la dernière fois, c'est que l'on peut **"cumuler"** les **mixins** !

Déclarons des bébés filles et des bébés garçons :

{% highlight coffeescript %}
function BabyBoy = -> DynamicObject():
	mixin(Mother()): mixin(Father())

function BabyGirl = -> DynamicObject():
	mixin(Father()): mixin(Mother())
{% endhighlight %}

Donc un `BabyBoy` "hérite" des caractéristiques de sa mère (`Mother`) puis de celles de son père (`Father`), donc si le père et la mère ont des caractéristiques communes, celles du père "prennent le pas" sur celle de la mère (une sorte de surcharge de caractéristique). Et inversement dans le cas des `BabyGirl`.

Laissons donc Fred et Wilma avoir des bébés en leur ajoutant quelques capacités "à la volée" :

{% highlight coffeescript %}
function main = |args| {

	var fred = Father(): initialize("Fred")
	var wilma = Mother(): initialize("Wilma")

	#Fred est devenu un adulte, il peut ...
	fred: define("gologolo", |this, flintstone| {
		if flintstone: sex() == "female" {
			flintstone: pregnant(true)	
		} else {
			raise("c'est pas gagné")
		}
	})

	#Wilma est une femme, elle peut avoir des enfants
	wilma: define("birthOfTwins", |this, girlsName, boysName| {
		if this: pregnant() {
			this: daughter(BabyGirl(): initialize(girlsName))
			this: son(BabyBoy(): initialize(boysName))
		}

	})

	#Fred & Wilma passent une bonne soirée
	fred: gologolo(wilma)

	#9 mois plus tard, Wilma accouche de jumeaux 
	wilma: birthOfTwins("Pebbles", "Junior")

	var pebbles = wilma: daughter()
	var junior = wilma: son()

	println(pebbles: hair() + " " + pebbles: lastName() + " " + pebbles: sex())
	println(junior: hair() + " " + junior: lastName() + " " + junior: sex())

	pebbles: fishing()
	pebbles: drawing()
	junior: fishing()
	junior: drawing() 

}
{% endhighlight %}

Et nous obtiendrons :

	red Flintstone female
	brown Flintstone male
	Pebbles is fishing
	Pebbles is drawing
	Junior is fishing
	Junior is drawing

Donc, Peebles et Junior ont bien hérités de caractéristiques en provenance à la fois des mâles et des femelles `FlintStone` (dessiner et pêcher), ainsi que de caractéristiques spécifiques à leur sexe (couleur de cheveux).

##Les enfants grandissent : l'apprentissage

Il n'y a pas que l'héritage pour obtenir des compétences, il y a aussi l'apprentissage. Tout ça pour dire que l'on peut faire aussi du **mixin** à la volée. Réinitialisons une `peebles` et un `junior`, et créons un DynamicObject `teacher` qui va pouvoir transférer ses connaissances en maths :

{% highlight coffeescript %}
function main = |args| {

	#bébés éprouvettes
	var pebbles = BabyGirl(): initialize("Pebbles")
	var junior = BabyBoy(): initialize("Junior")

	#C'est un prof de math
	var teacher = -> DynamicObject():
		knowledge("mathematics"):
		define("teaching", |this, somebody| {
			somebody:mixin(this)
		})

	#teatcher enseigne à pebbles
	teacher(): teaching(pebbles)	

	println("Pebbles loves " + pebbles: knowledge())

	#qui du coup va pouvoir elle même transférer son savoir à son frère
	pebbles: teaching(junior)

	#qui lui même ...
	println("Junior loves " + junior: knowledge())

}
{% endhighlight %}

Vous obtiendrez :

	Pebbles loves mathematics
	Junior loves mathematics

Donc nous venons de voir qu'il est possible de transférer facilement de DynamicObject en DynamicObject les membres de ceux-ci.

**Conclusion** : les mixins sont très puissants et permettent une approche fonctionnelle de l'héritage, et finalement le terme "héritage" seul n'est plus approprié. Si j'osais, je dirais *"une approche fonctionnelle de la vie"* ;)

*42.*

